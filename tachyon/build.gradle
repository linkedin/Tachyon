plugins {
    id 'com.android.library'
    id 'maven-publish'
    id 'com.jfrog.bintray' version '1.8.4'
}

group = GROUP_ID

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 1
        versionName VERSION_NAME
    }

    compileOptions {
        sourceCompatibility rootProject.ext.javaVersion
        targetCompatibility rootProject.ext.javaVersion
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation "androidx.fragment:fragment:1.0.0"

    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:2.22.0'
}

version = VERSION_NAME

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

publishing {
    publications {
        aar(MavenPublication) {

            artifact("$buildDir/outputs/aar/${project.getName()}-release.aar") {
                builtBy assemble
            }
            artifact sourcesJar
            artifact javadocJar

            pom.withXml {
                asNode().children().last() + {
                    resolveStrategy = Closure.DELEGATE_FIRST

                    description = LIBRARY_DESCRIPTION
                    url LIBRARY_GIT_URL
                    scm {
                        url LIBRARY_GIT_URL
                        connection LIBRARY_GIT_CONNECTION
                        developerConnection LIBRARY_GIT_CONNECTION
                    }
                    licenses {
                        license {
                            name '2-Clause BSD License'
                            url 'https://opensource.org/licenses/BSD-2-Clause'
                            distribution 'repo'
                        }
                    }

                    developers {
                        developer {
                            id 'com.linkedin'
                            name 'LinkedIn Corp.'
                            email ''
                        }
                    }

                    dependencies {
                        configurations.implementation.allDependencies.each { ModuleDependency dp ->
                            dependency {
                                resolveStrategy = Closure.DELEGATE_ONLY
                                groupId dp.group
                                artifactId dp.name
                                version dp.version
                            }
                        }
                    }
                }
            }
        }
    }
}

bintray {

    user = System.getenv('BINTRAY_USER') ?: ""
    key = System.getenv('BINTRAY_KEY') ?: ""

    pkg {
        userOrg = 'linkedin-tachyon'
        repo = 'maven'
        name = 'tachyon'
        desc = LIBRARY_DESCRIPTION
        labels = ['android']
        licenses = ['2-Clause BSD License']

        websiteUrl = LIBRARY_GIT_URL
        vcsUrl = LIBRARY_GIT_URL
        issueTrackerUrl = 'https://github.com/linkedin/Tachyon/issues'
        githubRepo = 'linkedin/tachyon'

        version {
            name = VERSION_NAME
            released = new Date()
        }

        publish = true
    }

    publications = ['aar']
}
